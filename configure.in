dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT(src/main.c)
AC_SUBST(VERSION, "0.7.2")
AC_CONFIG_SRCDIR(src/main.c)
AC_CONFIG_FILES([Makefile src/Makefile doc/Makefile])
AC_CONFIG_FILES([solaris/makepkg.sh solaris/pkginfo], [chmod +x solaris/makepkg.sh])

AM_INIT_AUTOMAKE(webchanges, $VERSION)
AM_CONFIG_HEADER(src/config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX	    # FIXME: check only if GUI requested
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for libraries.
AM_PATH_XML2(2.6.0, libxml2=1)
if test "${libxml2}" != 1; then
    AC_MSG_ERROR([
	libxml2 must be installed on your system
	but xml2-config script couldn't be found.

	Please check that xml2-config is in path, the directory
	where libxml2 libraries are installed (returned by
	'xml2-config --libs' command) is in LD_LIBRARY_PATH or
	equivalent variable and libxml2 version is 2.6.0 or above.
    ])
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(getopt.h errno.h string.h time.h sys/stat.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_REPLACE_FUNCS(getopt)
AC_CHECK_FUNCS([memset])

dnl [OPTION] Check, if webchanges should print HTML parsing-errors (libxml2)
AC_ARG_ENABLE(htmlerr, AC_HELP_STRING([--enable-html-errors], [Show HTML parsing-errors]), 
    [ if test "${enableval}" = yes; then
	AC_DEFINE([SHOW_HTML_ERRORS], [], [Show HTML parsing-errors])
    fi ])

dnl [OPTION] Check, if GUI has to be compiled
AC_ARG_ENABLE(gui, AC_HELP_STRING([--enable-gui], [Build with GUI]),
    [ if test "${enableval}" = yes; then
	gui=1
	AM_OPTIONS_WXCONFIG
	AM_PATH_WXCONFIG(2.4.0, wxwidgets=1)
	if test "${wxwidgets}" != 1; then
	    AC_MSG_ERROR([
	wxWidgets must be installed on your system
	but wx-config script couldn't be found.

	Please check that wx-config is in path, the directory
	where wxWidgets libraries are installed (returned by
	'wx-config --libs' command) is in LD_LIBRARY_PATH or
	equivalent variable and wxWidgets version is 2.4.0 or above.
	    ])
	fi
    fi ])
AM_CONDITIONAL(COMPILE_GUI, test "${gui}" = 1)

AC_OUTPUT
