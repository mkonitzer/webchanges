dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT(src/main.c)
AC_SUBST(VERSION, "0.8.0")
AC_CONFIG_SRCDIR(src/main.c)
AC_CONFIG_FILES([Makefile src/Makefile src/gmain-res.rc doc/Makefile])
AC_CONFIG_FILES([solaris/makepkg.sh solaris/pkginfo], [chmod +x solaris/makepkg.sh])

AM_INIT_AUTOMAKE(webchanges, $VERSION)
AM_CONFIG_HEADER(src/config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX	    # FIXME: check only if GUI requested
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN

dnl Checks for system-specific stuff.
AC_CANONICAL_HOST

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(getopt.h)

dnl Checks for library functions.

dnl Checks for libxml2 (mandatory).
AM_PATH_XML2(2.6.0,,AC_MSG_ERROR([*** libxml2 and libxml2-dev >=2.6.0 are required to build webchanges ***]))

dnl Checks for libcurl (optional).
LIBCURL_CHECK_CONFIG([yes],[7.0],[check_curl=yes],[check_curl=no])

dnl [OPTION] SHOW_HTML_ERRORS
AC_ARG_ENABLE(htmlerr, AC_HELP_STRING([--enable-html-errors], [Show HTML-errors]), if test $enableval = yes; then AC_DEFINE([SHOW_HTML_ERRORS], [], [Show HTML-errors]) fi)

dnl [OPTION] Check, if GUI has to be compiled
AC_ARG_VAR(WINDRES, [windres tool path (for WIN32 GUI only)])
AC_ARG_ENABLE(gui, AC_HELP_STRING([--enable-gui], [Build with GUI]),
    [ if test "${enableval}" = yes; then
	gui=1
	AC_CHECK_PROG(cxx_present, $CXX, "yes", "no")
	if test "x$cxx_present" != "xyes"; then
	    AC_MSG_ERROR([*** No C++ compiler can be found. You need a C++ compiler to compile GUI.])
	fi
	AM_OPTIONS_WXCONFIG
	AM_PATH_WXCONFIG(2.4.0, wxwidgets=1)
	if test "${wxwidgets}" != 1; then
	    AC_MSG_ERROR([
	wxWidgets must be installed on your system
	but wx-config script couldn't be found.

	Please check that wx-config is in path, the directory
	where wxWidgets libraries are installed (returned by
	'wx-config --libs' command) is in LD_LIBRARY_PATH or
	equivalent variable and wxWidgets version is 2.4.0 or above.
	    ])
	fi

	dnl Check for windres executable if needed.
	AC_MSG_CHECKING([whether windres is needed])
	case "${host}" in
	    *-mingw32*|*cygwin*)
		AC_MSG_RESULT([yes])

		dnl Try to find windres executable.
		if test "x$WINDRES" != "x"; then
		    AC_PATH_PROG([windres], [$WINDRES])
		else
		    AC_CHECK_TOOL([windres], [windres])
		fi
		if test "x$windres" = "x"; then
		    AC_MSG_ERROR([*** 'windres' could not be found, please make sure this program is within your path or set \$WINDRES manually.])
		fi
		WINDRES=$windres
		gui_resfile=1

		dnl Check for windres include flags if needed.
		WINDRESFLAGS=""
		for opt in $WX_CPPFLAGS
		do
		    case "$opt" in
		    -I*)
			WINDRESFLAGS="$WINDRESFLAGS --include-dir=${opt#-I}"
			;;
		    -D*)
			WINDRESFLAGS="$WINDRESFLAGS $opt"
			;;
		    esac
		done
		;;
	    *)
		AC_MSG_RESULT([no])
		;;
	esac
	AC_SUBST(WINDRES)
	AC_SUBST(WINDRESFLAGS)

	dnl Translate version string to resource file format.
	vnumber=${VERSION%-*}
	vnum_maj=${vnumber%%.*}
	vnumber=${vnumber#${vnum_maj}.}
	vnum_min=${vnumber%%.*}
	vnumber=${vnumber#${vnum_min}.}
	vnum_mic=${vnumber%%.*}
	AC_SUBST(RESVERSION,[${vnum_maj},${vnum_min},${vnum_mic},0])
    fi ])
AM_CONDITIONAL(COMPILE_GUI, test "${gui}" = 1)
AM_CONDITIONAL(COMPILE_GUI_RESOURCE, test "${gui_resfile}" = 1)

AC_OUTPUT
